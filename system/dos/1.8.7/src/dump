PACKET dump DEFINES 
 
  dump:

TEXT VAR ergebnis := "";

PROC dump (TEXT CONST kommentar, dump text):
  ergebnis := kommentar;
  ergebnis CAT ": ";
  INT VAR i;
  FOR i FROM 1 UPTO LENGTH dump text REP
    zeichen schreiben
  PER;
  ergebnis schreiben.

zeichen schreiben:
  INT CONST char code :: code (dump text SUB i);
  IF char code < 32
     THEN ergebnis CAT ("$" + text (char code) + "$")
     ELSE ergebnis CAT code (char code)
  FI.

END PROC dump;

PROC dump (TEXT CONST kommentar, INT CONST dump int):
  ergebnis := kommentar;
  ergebnis CAT ": ";
  ergebnis CAT text (dump int);
  ergebnis schreiben.

END PROC dump;

PROC dump (TEXT CONST kommentar, REAL CONST dump real):
  ergebnis := kommentar;
  ergebnis CAT ": ";
  ergebnis CAT text (dump real);
  ergebnis schreiben.

END PROC dump;

PROC ergebnis schreiben:
  FILE VAR f := sequential file (output, "logbuch");
  putline (f, ergebnis);
  ergebnis := "".

END PROC ergebnis schreiben;

END PACKET dump;

