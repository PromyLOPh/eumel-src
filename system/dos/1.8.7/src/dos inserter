IF NOT single user
  THEN do ("IF name (myself) <> ""DOS"" THEN error stop (""Bitte der Task den Namen 'DOS' geben und neu starten"") FI");
FI; 
 
archive ("austausch");
check off;
command dialogue (FALSE);
hol ("shard interface");
hol ("bpb ds");
hol ("insert.dos");
IF single user
  THEN do (PROC (TEXT CONST) gen s, ALL "insert.dos");
       gen s ("manager/S.dos")
  ELSE do (PROC (TEXT CONST) hol, ALL "insert.dos");
       hol ("manager/M.dos");
       release (archive);
       do (PROC (TEXT CONST) gen m, ALL "insert.dos");
       gen m ("manager/M.dos");
       putline ("jetzt mit 'dos manager' bzw. 'dos manager (channnel)' starten");
FI;
do ("hd version (FALSE)");
do ("load shard interface table");
forget ("shard interface", quiet);
forget ("insert.dos", quiet);
forget ("dos inserter", quiet).
 
single user:
  (pcb (9) AND 255) = 1.
 
PROC gen m (TEXT CONST name):
  insert (name);
  forget (name, quiet)
 
END PROC gen m;
 
PROC gen s (TEXT CONST t):
  hol (t);
  insert (t);
  forget (t, quiet)
 
END PROC gen s;
 
PROC hol (TEXT CONST t):
  IF NOT exists (t)
    THEN fetch (t, archive)
  FI
 
END PROC hol;
 
 
 
 
 
 
 
 
 


