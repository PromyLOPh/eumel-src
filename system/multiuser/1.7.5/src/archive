(* ------------------- VERSION 14     06.03.86 ------------------- *)
PACKET archive DEFINES

               archive ,
               clear ,
               release ,
               format ,
               check ,
               reserve :


LET clear code      = 18 ,
    reserve code    = 19 ,
    free code       = 20 ,
    check read code = 22 ,
    format code     = 23 ;


TASK PROC archive :
 
  task ("ARCHIVE")

ENDPROC archive ;

PROC archive (TEXT CONST archive name, TASK CONST task) :

  call (reserve code, archive name, task)

ENDPROC archive ;

PROC reserve (TEXT CONST message, TASK CONST task) :

  call (reserve code, message, task)

END PROC reserve;

PROC reserve (TASK CONST task) :

  call(reserve code, "", task)

END PROC reserve;

PROC archive (TEXT CONST archive name, INT CONST station) :

  call (reserve code, archive name, station/ "ARCHIVE")

ENDPROC archive ;

PROC archive (TEXT CONST archive name):

  call (reserve code, archive name, archive)

ENDPROC archive ;

PROC release (TASK CONST task) :

  call (free code, "", task)

ENDPROC release ;

PROC clear (TASK CONST task) :

  call (clear code, "", task)

ENDPROC clear ;

PROC format (TASK CONST task) :

  format (0, task)

ENDPROC format ;

PROC format (INT CONST code, TASK CONST task) :

  call (format code , text (code), task)

ENDPROC format ;

PROC check (TEXT CONST file name, TASK CONST task) :

  call (check read code, file name, task)

ENDPROC check ;

PROC check (THESAURUS CONST nameset, TASK CONST task) :

  do (PROC (TEXT CONST, TASK CONST) check, nameset, task)

ENDPROC check ;

ENDPACKET archive ;

