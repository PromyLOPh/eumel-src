PACKET emulator 16 DEFINES                     (* Autor:  J.Liedtke *)
                                               (* Stand:  11.10.83  *)
  killer ,                                     (* F. Klapper, 26.03.84 *) 
  command handler ,
  set command ,
  to archive, 
  from archive, 
  load archive, 
  save archive, 
  list archive, 
  release archive: 
 
PROC list archive: 
  list (archive) 
 
END PROC list archive; 
 
PROC release archive: 
  release (archive) 
 
END PROC release archive; 
 
PROC to archive: 
  save (last param, archive) 
 
END PROC to archive; 
 
PROC to archive (TEXT CONST t): 
  save (t, archive) 
 
END PROC to archive; 
 
PROC from archive (TEXT CONST t): 
  fetch (t, archive) 
 
END PROC from archive; 
 
PROC load archive: 
  fetch all (archive) 
 
END PROC load archive; 
 
PROC save to archive (THESAURUS CONST thes): 
  disable stop; 
  all to archive (thes); 
  IF is error 
  THEN put error; 
       line; 
       clear error; 
       IF yes ("naechste Archivfloppy eingelegt") 
       THEN save to archive (remainder) 
       FI 
   FI; 
   enable stop 
 
END PROC save to archive; 
 
PROC all to archive (THESAURUS CONST thes): 
  enable stop; 
  save (thes, archive) 
 
END PROC all to archive; 
 
PROC save archive: 
  save to archive (ALL myself) 
 
END PROC save archive; 
 
PROC save archive (TEXT CONST liste): 
  save to archive (ALL liste) 
 
END PROC save archive; 
 
PROC killer :
  forget (ALL myself)

ENDPROC killer ;

TEXT VAR command line;
INT VAR permitted type := 0 ;

PROC set command (TEXT CONST command text, INT CONST type) :

  command line := command text;
  permitted type := type

ENDPROC set command ;

PROC command handler (TEXT CONST command list, 
                      INT VAR command index , number of params ,
                      TEXT VAR param 1, param 2) :

  analyze command (command list, command line, permitted type, command index,
                   number of params, param 1, param 2) 

ENDPROC command handler ;
 
PROC command handler (TEXT CONST command list, 
                      INT VAR command index , number of params ,
                      TEXT VAR param 1, param 2,
                      TEXT CONST command text) :
 
  get command (command text, command line) ;
  analyze command (command list, command line, 0,
                   command index, number of params, param 1, param 2) 

ENDPROC command handler ;

ENDPACKET emulator 16 ;
