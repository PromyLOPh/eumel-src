
PACKET elan do interface DEFINES                  (*Autor: J.Liedtke *)
                                                  (*Stand: 08.11.85  *)
    do ,
    no do again :
 
 
LET no ins = FALSE ,
    no lst = FALSE ,
    no check = FALSE ,
    no sermon = FALSE ,
    compile line mode = 2 ,
    do again mode = 4 ,
    max command length = 2000 ;


INT VAR do again mod nr := 0 ;
TEXT VAR previous command := "" ;

DATASPACE VAR ds ;

 
PROC do (TEXT CONST command) :

  enable stop ;
  IF LENGTH command > max command length
    THEN errorstop ("Kommando zu lang")
  ELIF do again mod nr <> 0 AND command = previous command
    THEN do again
    ELSE previous command := command ;
         compile and execute
  FI .

do again :
  elan (do again mode, ds, "", do again mod nr,
        no ins, no lst, no check, no sermon) .

compile and execute :
  elan (compile line mode, ds, command, do again mod nr,
        no ins, no lst, no check, no sermon) .

ENDPROC do ;
 
PROC no do again :

  do again mod nr := 0 

ENDPROC no do again ;
 
PROC elan (INT CONST mode, DATASPACE CONST source, TEXT CONST line, 
           INT VAR start module number,
           BOOL CONST ins, lst, rt check, ser) :
  EXTERNAL 256
ENDPROC elan ;

ENDPACKET elan do interface ;

