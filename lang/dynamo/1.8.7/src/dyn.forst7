#papersize(20.5,61.0)# 
#pagelength(59.0)# 
#type("picac")# 
NOTE     25.04.1985  14.30 
note     forstbetriebsmodell 
note 
note  stehender Holzvorrat 
l        holz.k=holz.j+dt*(zuwachs.jk-ernte.jk) 
n        holz=startholz 
c        startholz=15000        Angabe in Festmetern(fm)
r        zuwachs.kl=holz.k*vorratsfaktor*pflegefaktor.k 
c        vorratsfaktor=0.035    Zuwachs in % von holz
a        pflegefaktor.k=tabhl(tpffaktor,pfzustand.k,0,1,0.1) 
t        tpffaktor=.7/.7/.7/.7/.7/.8/.9/.95/1/1.05/1.2 
r        ernte.kl=ernte1.k 
a        ernte1.k=holz.k*ernteproz.k*ernteprozfak.k
a        ernteproz.k=tabhl(ternproz,ratio1.k,0.5,1.2,0.1) 
t        ternproz=0.02/0.025/0.03/0.03/0.03/0.035/0.06/0.08 
a        ratio1.k=holz.k/maxvorrat 
c        maxvorrat=16000 
note 
note  resourcen 
l        resourcen.k=resourcen.j+dt*(einnahme.jk-eig.jk-pfausgaben.jk)
n        resourcen=startresourcen 
c        startresourcen=100000     Geldeinheiten 
c        preis=190                 Geldeinheiten pro fm 
r        einnahme.kl=ernte1.k*preis
r        pfausgaben.kl=resourcen.k-eigenent.k
r        eig.kl=eigenent.k
a        anpassungsfaktor.k=tabhl(tanpass,ratio2.k,0.5,1.5,0.1) 
t        tanpass=.5/.55/.6/.7/.9/1/1/1/1.1/1.2/1.3
l        eigenent.k=min(eigenent.j*anpass.jk,resourcen.j)
r        anpass.kl=anpassungsfaktor.k 
n        eigenent=eigenentstart 
c        eigenentstart=20000 
note 
note  arbeitseinheiten
note
l        arbeit.k=arbeit.j+dt*(pfausgaben.jk/preisae-arbeitsverbrauch.jk)
n        arbeit=startarbeit 
c        startarbeit=800
c        preisae=100 ( preis pro arbeitseinheit in geldeinheiten ) 
r        arbeitsverbrauch.kl=min(arbeit.k,notwarbeit.k)
a        notwarbeit.k=tabhl(tnotwarb,pfzustand.k,0.0,1.0,0.1) 
t        tnotwarb=1600/1550/1500/1450/1400/1300/1150/950/800/700/600 
a        ratio2.k=arbeit.k/notwarbeit.k
a        ernteprozfak.k=tabhl(ternteprozfak,ratio2.k,0.2,1.6,0.2) 
t        ternteprozfak=.4/.5/1/2/1.05/1/.9/.7
note 
note  Pflegezustand (pfzustand; dimensionslose Größe) 
l        pfzustand.k=pfzustand.j+dt*(pfverbess.jk-pfversch.jk) 
n        pfzustand=startpfzustand 
c        startpfzustand=0.8
r        pfverbess.kl=smooth(pfx1.k,pfverzoeg) 
a        pfx1.k=pfverbfaktor.k*(1-pfzustand.k)
a        pfverbfaktor.k=tabhl(tpfverb,ratio2.k,.8,1.4,.2) 
t        tpfverb=0/0/.1/0.2
r        pfversch.kl=smooth(pfverschfaktor.k*pfzustand.k,pfverzoeg)
c        pfverzoeg=2 
a        pfverschfaktor.k=tabhl(tpfversch,ratio2.k,0,.8,.2) 
t        tpfversch=.4/.2/.1/.05/0 
note 
note 
note  print    ratio1,ratio2,eigenent,arbeit,pfzustand 
plot     holz=h(1e4,2e4)/eigenent=e(0,2e5)/pfzustand=P(0,1)/ratio2=2(0,5)
c        dt=1 
c        length=50 
note     prtper=1 
c        pltper=1
run  dyn.forst7






